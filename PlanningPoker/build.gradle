import org.gradle.api.file.DuplicatesStrategy

apply plugin: 'war'
apply plugin: 'eclipse-wtp'

java {
    sourceCompatibility = 21
    targetCompatibility = 21
}

repositories {
    mavenCentral()
}

wrapper {
	gradleVersion = '9.0.0'
}

configurations {
	embeddedJetty
}

configurations.all {
	resolutionStrategy {
	  // cache dynamic versions for 10 minutes
	  cacheDynamicVersionsFor 10*60, 'seconds'
	  // don't cache changing modules at all
	  cacheChangingModulesFor 0, 'seconds'
	}
  }

dependencies {
    // Jetty 12
    embeddedJetty 'org.eclipse.jetty.ee10:jetty-ee10-servlet:12.0.10'
    embeddedJetty 'org.eclipse.jetty.ee10:jetty-ee10-webapp:12.0.10'
    embeddedJetty 'org.eclipse.jetty.ee10:jetty-ee10-plus:12.0.10'
    embeddedJetty 'org.eclipse.jetty.ee10:jetty-ee10-annotations:12.0.10'
    embeddedJetty 'org.eclipse.jetty.ee10:jetty-ee10-apache-jsp:12.0.10'
    embeddedJetty 'org.eclipse.jetty.ee10.websocket:jetty-ee10-websocket-jetty-server:12.0.10'

    // JSON library
    implementation 'org.glassfish:jakarta.json:2.0.1'

    // Testing
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.10.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.10.0'
    // Needed for parameterized tests
    testImplementation 'org.junit.jupiter:junit-jupiter-params:5.10.0'
    // Needed if you want to use the launcher for other purposes
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

    // Mockito
    testImplementation 'org.mockito:mockito-core:5.10.0'
    testImplementation 'org.mockito:mockito-junit-jupiter:5.10.0'
}


test {
    useJUnitPlatform()
}


task mytask {
	doLast {
		println configurations.embeddedJetty.resolutionStrategy.forcedModules
	}
}

war.archiveBaseName = 'PlanningPoker'
war {
    destinationDirectory = file("/tmp")
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
	// unzip and add all jetty dependencies into the root of our war file
	from {configurations.embeddedJetty.collect {
			project.zipTree(it)
		}
	}
	// remove signature and unnecessary files
	exclude "META-INF/*.SF", "META-INF/*.RSA", "about.html", "about_files/**", "readme.txt", "plugin.properties", "jetty-dir.css"

	// include only the classes which will be used to start Embedded Jetty
	from "$buildDir/classes/main"
	exclude "com/myapp/"
	
	// tells the class to run when the generate war be executed using 'java -jar'
	manifest { attributes 'Main-Class': 'com.embedded.JettyStarter' }

	doLast {
		println "WAR file created at: " + archiveFile.get().asFile.absolutePath
	}
}

// Once you will need some basic api (e.i. servlet api) for compilation, add embeddedJetty dependencies for compilation
sourceSets.main.compileClasspath += configurations.embeddedJetty

// Test classpath should also include embeddedJetty dependencies
sourceSets.test.compileClasspath += configurations.embeddedJetty
sourceSets.test.runtimeClasspath += configurations.embeddedJetty

// the same for eclipse classpath, so you can use it to edit your java files
eclipse {
	classpath {
		plusConfigurations += configurations.embeddedJetty
	}
}
